!######################################################################
! This routine is part of
! RuNNer - Ruhr-University Neural Network Energy Representation
! (c) Dr. Joerg Behler 2008
!######################################################################

!! called by: 
!! - predict.f90
!!
      subroutine writepdb(num_atoms,&
           lattice,xyzstruct,elementsymbol,lperiodic)
!!
      use fileunits
      use globaloptions
      use nnconstants
!!
      implicit none
!!
      integer num_atoms                ! in
      integer i1                       ! internal
!! 
      real*8 lattice(3,3)               ! in
      real*8 xyzstruct(3,max_num_atoms) ! in
      real*8 a                          ! internal
      real*8 b                          ! internal
      real*8 c                          ! internal
      real*8 alpha                      ! internal
      real*8 beta                       ! internal
      real*8 gamm                       ! internal
!!
      character*2 elementsymbol(max_num_atoms) ! in
!!
      logical lperiodic                ! in
!!
!!
!! Note: Angstrom length unit
!! Caution: pdbs are fixed formatted
!!
      open(pdbunit,file='structure.pdb',form='formatted',status='replace')
!!
      if(lperiodic)then
!! pdb requires a,b,c,alpha,beta,gamma instead of lattice vectors
        a=lattice(1,1)**2 + lattice(1,2)**2 + lattice(1,3)**2
        a=dsqrt(a)
        a=a*0.529177d0
        b=lattice(2,1)**2 + lattice(2,2)**2 + lattice(2,3)**2
        b=dsqrt(b)
        b=b*0.529177d0
        c=lattice(3,1)**2 + lattice(3,2)**2 + lattice(3,3)**2
        c=dsqrt(c)
        c=c*0.529177d0
!! calculate angles properly!!!
        alpha=abs(lattice(2,1)*lattice(3,1)+lattice(2,2)*lattice(3,2)+lattice(2,3)*lattice(3,3))/(b*c)
        alpha=acos(alpha)
        alpha=alpha*180.d0/pi
        beta =abs(lattice(1,1)*lattice(3,1)+lattice(1,2)*lattice(3,2)+lattice(1,3)*lattice(3,3))/(a*c)
        beta =acos(beta) 
        beta =beta*180.d0/pi
        gamm =abs(lattice(1,1)*lattice(2,1)+lattice(1,2)*lattice(2,2)+lattice(1,3)*lattice(2,3))/(a*b)
        gamm =acos(gamm)
        gamm =gamm*180.d0/pi
!!
        write(pdbunit,'(a)')'pdb generated by RuNNer'
        write(pdbunit,'(a6,3f9.3,3f7.2,1x,a15)')"CRYST1 ",a,b,c,alpha,beta,gamm,"P 1           1"
        write(pdbunit,'(a)')'MODEL 0'
        do i1=1,num_atoms 
          write(pdbunit,201) i1,elementsymbol(i1),&
          i1,xyzstruct(1,i1)*0.529177d0,xyzstruct(2,i1)*0.529177d0,&
          xyzstruct(3,i1)*0.529177d0
        enddo
        write(pdbunit,'(a)')'TER'
        write(pdbunit,'(a)')'ENDMDL'
      else ! lperiodic
        write(pdbunit,'(a)')'pdb generated by RuNNer'
        do i1=1,num_atoms 
          write(pdbunit,201) i1,elementsymbol(i1),&
          i1,xyzstruct(1,i1)*0.529177d0,xyzstruct(2,i1)*0.529177d0,&
          xyzstruct(3,i1)*0.529177d0
        enddo
      endif ! lperiodic
!!
      close(pdbunit)
201 FORMAT ("HETATM",i5,a3,3x,"DCB",i6,4x,3f8.3,"  1.00  0.00")
!!
      return
      end
