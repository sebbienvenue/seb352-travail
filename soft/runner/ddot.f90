!*> \brief \b DDOT
!*
!* =========== DOCUMENTATION ===========
!*
!* Online html documentation available at
!* http://www.netlib.org/lapack/explore-html/
!*
!* Definition:
!* ===========
!*
!* DOUBLE PRECISION FUNCTION DDOT(N,DX,INCX,DY,INCY)
!*
!* .. Scalar Arguments ..
!* INTEGER INCX,INCY,N
!* ..
!* .. Array Arguments ..
!* DOUBLE PRECISION DX(*),DY(*)
!* ..
!*
!*
!*> \par Purpose:
!* =============
!*>
!*> \verbatim
!*>
!*> DDOT forms the dot product of two vectors.
!*> uses unrolled loops for increments equal to one.
!*> \endverbatim
!*
!* Authors:
!* ========
!*
!*> \author Univ. of Tennessee
!*> \author Univ. of California Berkeley
!*> \author Univ. of Colorado Denver
!*> \author NAG Ltd.
!*
!*> \date November 2011
!*
!*> \ingroup double_blas_level1
!*
!*> \par Further Details:
!* =====================
!*>
!*> \verbatim
!*>
!*> jack dongarra, linpack, 3/11/78.
!*> modified 12/3/93, array(1) declarations changed to array(*)
!*> \endverbatim
!*>
!* =====================================================================
      DOUBLE PRECISION FUNCTION ddot(N,DX,INCX,DY,INCY)
!*
!* -- Reference BLAS level1 routine (version 3.4.0) --
!* -- Reference BLAS is a software package provided by Univ. of Tennessee, --
!* -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!* November 2011
!*
!* .. Scalar Arguments ..
      INTEGER incx,incy,n
!* ..
!* .. Array Arguments ..
      DOUBLE PRECISION dx(*),dy(*)
!* ..
!*
!* =====================================================================
!*
!* .. Local Scalars ..
      DOUBLE PRECISION dtemp
      INTEGER i,ix,iy,m,mp1
!* ..
!* .. Intrinsic Functions ..
      INTRINSIC mod
!* ..
      ddot = 0.0d0
      dtemp = 0.0d0
      IF (n.LE.0) RETURN
      IF (incx.EQ.1 .AND. incy.EQ.1) THEN
!*
!* code for both increments equal to 1
!*
!*
!* clean-up loop
!*
      m = mod(n,5)
      IF (m.NE.0) THEN
      DO i = 1,m
      dtemp = dtemp + dx(i)*dy(i)
      END DO
      IF (n.LT.5) THEN
      ddot=dtemp
      RETURN
      END IF
      END IF
      mp1 = m + 1
      DO i = mp1,n,5
      dtemp = dtemp + dx(i)*dy(i) + dx(i+1)*dy(i+1) + &
       dx(i+2)*dy(i+2) + dx(i+3)*dy(i+3) + dx(i+4)*dy(i+4)
      END DO
      ELSE
!*
!* code for unequal increments or equal increments
!* not equal to 1
!*
      ix = 1
      iy = 1
      IF (incx.LT.0) ix = (-n+1)*incx + 1
      IF (incy.LT.0) iy = (-n+1)*incy + 1
      DO i = 1,n
      dtemp = dtemp + dx(ix)*dy(iy)
      ix = ix + incx
      iy = iy + incy
      END DO
      END IF
      ddot = dtemp
      RETURN
      END
